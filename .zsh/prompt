#!/bin/zsh
# dotfiles: collection of my personal dotfiles [code]
# Copyright (C) 2012-2018 Aleksa Sarai <cyphar@cyphar.com>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Enable prompt substitution and colours.
autoload -U colors; colors
setopt promptsubst

# Emacs mode.
bindkey -e

# Set up prompt.
function __generate_prompt() {
	# Save old exit code first.
	local exit_code="$?"

	# Locals.
	local user_prompt
	local path_prompt
	local ret_prompt

	# User information.
	if [[ "${EUID}" == "0" ]]; then
		user_prompt="%{$fg_bold[red]%}%M%{$reset_color%}"
	else
		user_prompt="%{$fg_bold[green]%}%n%{$reset_color%}%{$fg[green]%}@%M%{$reset_color%}"
	fi

	# Git branch and ref information.
	# Everything is calculated inside this shell script (no calls to `git` are
	# made). This is because on large projects (like, say, the kernel) git can
	# take >1 minute to figure out what branch and ref HEAD is pointing to. The
	# filesystem is faster. However, we have to fall back in *one* case, when
	# we are sitting on a packed ref or a repo which was recently gc'd -- in
	# that case `git` is faster and easier.
	function __git_info() {
		function __find_git() {
			local current="$1"

			# Search up the directory tree for '.git'.
			while [[ "${current}" != "/" ]]; do
				if [[ -d "${current}/.git" ]]; then
					echo "${current}/.git"
					return 0
				fi

				current="$(dirname -- "${current}")"
			done

			# Check for '/.git'.
			if [[ -d "${current}/.git" ]]; then
				echo "${current}/.git"
				return 0
			fi

			return 1
		}

		# Get the current branch ref from '.git/HEAD' and '.git/refs/heads'.
		function __get_branch() {
			local gitdir="$1"

			if (grep -v "^ref: refs/heads/.*$" -- "${gitdir}/HEAD" &>/dev/null); then
				# Detached state -- no branch name.
				return 1
			fi

			# Get the ref path.
			local refbranch="$(cut -d' ' -f2 -- "${gitdir}/HEAD")"

			# Yield the actual branch name from the refbranch.
			echo "${refbranch#refs/heads/}"
			return 0
		}

		# Get short commit hash.
		function __get_short_ref() {
			local gitdir="$1"
			local refhash

			# Detached state -- ref is in 'ref'.
			if ! (__get_branch "${gitdir}" &>/dev/null); then
				refhash="$(< "${gitdir}/HEAD")"
			else
				# Get the ref path.
				local refbranch="$(cut -d' ' -f2 -- "${gitdir}/HEAD")"

				# Check that the refbranch exists.
				if ! [[ -f "${gitdir}/${refbranch}" ]]; then
					# If not, we might be on a commit in a packed ref.
					# Fallback to `git` in this case -- we can't do better.
					refhash="$(git rev-parse --verify HEAD)"

					# We don't have a commit.
					if [[ "$?" != 0 ]]; then
						return 1
					fi
				else
					# Get the commit hash from the ref path.
					refhash="$(< "${gitdir}/${refbranch}")"
				fi
			fi

			# Shorten the ref to 12 characters.
			# Why 12? Because that's what the kernel tells us is good.
			# In Linus we trust.
			echo "$refhash[1,12]"
			return 0
		}

		# Most shells (zsh included) like to be clever with symlinks. This breaks
		# things like symlinks into subdirectories of git repositories (because
		# `pwd` lies to you). Fix this by using the `pwd` command which isn't
		# magical.
		local real_pwd="$(command pwd)"

		if (__find_git "${real_pwd}" &>/dev/null); then
			# Get the closest git directory.
			local gitdir="$(__find_git "${real_pwd}")"

			# Figure out if in detached state.
			local refinfo
			if (__get_branch "${gitdir}" &>/dev/null); then
				refinfo="%{$fg_bold[magenta]%}$(__get_branch "${gitdir}")%{$reset_color%}"
			else
				refinfo="%{$fg_bold[red]%}{detached}%{$reset_color%}"
			fi

			# Figure out short ref.
			local refhash
			if (__get_short_ref "${gitdir}" &>/dev/null); then
				refhash="%{$fg[magenta]%}$(__get_short_ref "${gitdir}")%{$reset_color%}"
			else
				refhash="%{$fg[red]%}000000000000%{$reset_color%}"
			fi

			echo " git:(${refinfo}:${refhash})"
		fi
	}

	# Mercurial branch information.
	function __hg_info() {
		# Hot path: check that there isn't a parent .hg/ directory.
		# We don't implement parsing (I don't clone big hg project often enough
		# to care).
		function __find_hg() {
			local current="$1"

			# Search up the directory tree for '.git'.
			while [[ "${current}" != "/" ]]; do
				if [[ -d "${current}/.hg" ]]; then
					echo "${current}/.hg"
					return 0
				fi

				current="$(dirname -- "${current}")"
			done

			# Check for '/.hg'.
			if [[ -d "${current}/.hg" ]]; then
				echo "${current}/.hg"
				return 0
			fi

			return 1
		}

		# Most shells (zsh included) like to be clever with symlinks. This breaks
		# things like symlinks into subdirectories of git repositories (because
		# `pwd` lies to you). Fix this by using the `pwd` command which isn't
		# magical.
		local real_pwd="$(command pwd)"

		if (__find_hg "${real_pwd}" && hg status &>/dev/null); then
			local refbranch="$(hg branch)"
			local refhash="$(hg id | cut -d' ' -f1)"

			echo " hg:(%{$fg_bold[magenta]%}${refbranch}%{$reset_color%}:%{$fg[magenta]%}${refhash}%{$reset_color%})"
		fi
	}

	# Get the last part of the given path, with tilde replacement.
	function __nice_path() {
		local current_path="$1"

		if [[ "${current_path}" == "${HOME}" ]]; then
			current_path="~"
		else
			current_path="$(basename "${current_path}")"
		fi

		echo "${current_path}"
	}

	# Path information.
	path_prompt="%{$fg_bold[blue]%}$(__nice_path "$(pwd)")%{$reset_color%}$(__git_info)$(__hg_info)"

	# Return prompt based on exit code of last command.
	function __ret_prompt() {
		local exit_code="$1"
		local prompt="%#"

		if [[ "${exit_code}" != "0" ]]; then
			prompt="%{$fg_bold[red]%}${prompt}%{$reset_color%}"
		fi

		echo "${prompt} "
	}
	ret_prompt="$(__ret_prompt "${exit_code}")"

	# Get loadavg for last minute.
	function __loadavg() {
		[[ -f "/proc/loadavg" ]] && echo "$(cat /proc/loadavg 2>/dev/null | cut -d' ' -f1)"
	}

	# Get number of processes, if we can.
	function __nproc() {
		echo "$(nproc 2>/dev/null || echo '?')"
	}

	# Actually set up prompts.
	export PROMPT="$user_prompt %{$fg[cyan]%}::%{$reset_color%} $path_prompt $ret_prompt"
	export RPROMPT="[$(uname -o) $(__loadavg)($(__nproc))]"

	# Set up secondary prompts.
	export PROMPT2="%_> "
	export PROMPT3="?#> "
	export PROMPT4="+> "
}

# Set up precmd hook to set up prompt.
function precmd() {
	__generate_prompt
}
